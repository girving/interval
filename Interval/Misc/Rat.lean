import Mathlib.Algebra.Order.Floor.Div
import Mathlib.Data.Real.Basic
import Interval.Misc.Bool
import Interval.Misc.Nat
import Interval.Misc.Real

/-!
## `â„š` machinery
-/

open Set
variable {ğ•œ : Type} [Field ğ•œ] [LinearOrder ğ•œ] [IsStrictOrderedRing ğ•œ]

lemma Rat.abs_eq_div {x : â„š} : |x| = (x.num.natAbs : â„š) / x.den := by
  nth_rw 1 [â†Rat.num_div_den x]
  have d0 : 0 < (x.den : â„š) := Nat.cast_pos.mpr x.den_pos
  rw [abs_div, abs_of_pos d0, â†Int.cast_abs, Int.abs_eq_natAbs, Int.cast_natCast]

lemma Rat.abs_eq_div' {x : â„š} : (|x| : ğ•œ) = (x.num.natAbs : ğ•œ) / x.den := by
  nth_rw 1 [â†Rat.num_div_den x]
  have d0 : 0 < (x.den : ğ•œ) := Nat.cast_pos.mpr x.den_pos
  simp only [cast_div, cast_intCast, cast_natCast, abs_div, abs_of_pos d0, â†Int.cast_abs,
    Int.abs_eq_natAbs, Int.cast_natCast]

/-- `n` s.t. `2^n â‰¤ |x| < 2^(n+1)` if `n â‰  0` -/
@[irreducible] def Rat.log2 (x : â„š) : â„¤ :=
  -- Reduce to two possible answers
  let n := x.num.natAbs
  let a := n.fast_log2  -- `2^a â‰¤ n < 2^(a+1)`
  let b := x.den.fast_log2  -- `2^b â‰¤ d < 2^(b+1)`
  -- `2^(a-b-1) < n/d < 2^(a+1-b)`, so the answer is either `a-b-1` or `a-b`
  -- If `2^(a-b) â‰¤ n/d` then `a-b`, otherwise `a-b-1`
  let g : â„¤ := a - b
  let good := bif 0 â‰¤ g then decide (x.den <<< g.toNat â‰¤ n) else decide (x.den â‰¤ n <<< (-g).toNat)
  bif good then g else g - 1

lemma Rat.log2_correct {x : â„š} (x0 : x â‰  0) : |x| âˆˆ Ico (2^x.log2) (2^(x.log2 + 1)) := by
  have t0 : (2:â„š) â‰  0 := by norm_num
  rw [log2]
  simp only [sub_nonneg, Nat.cast_le, neg_sub, bif_eq_if, decide_eq_true_eq, Nat.shiftLeft_eq,
    Nat.fast_log2_eq]
  generalize hn : x.num.natAbs = n
  generalize ha : n.log2 = a
  generalize hb : x.den.log2 = b
  have n0 : n â‰  0 := by rwa [â†hn, Int.natAbs_ne_zero, Rat.num_ne_zero]
  have d0' : 0 < (x.den : â„š) := Nat.cast_pos.mpr x.den_pos
  have an := Nat.log2_self_le n0
  have bd := Nat.log2_self_le x.den_nz
  have na := Nat.lt_log2_self (n := n)
  have db := Nat.lt_log2_self (n := x.den)
  simp only [ha, hb] at an bd na db
  have ae : |x| = (n : â„š) / x.den := by rw [Rat.abs_eq_div, hn]
  have lo : 2^(a - b - 1 : â„¤) â‰¤ |x| := by
    rw [ae]
    refine le_trans ?_ (div_le_divâ‚€ (by positivity) (Nat.cast_le.mpr an) (by positivity)
      (Nat.cast_le.mpr db.le))
    simp only [sub_sub, zpow_subâ‚€ t0, zpow_natCast, Nat.cast_pow, Nat.cast_ofNat,
      â† Nat.cast_add_one, le_refl]
  have hi : |x| < 2^(a - b + 1 : â„¤) := by
    rw [ae]
    refine lt_of_lt_of_le ((div_lt_div_iff_of_pos_right d0').mpr (Nat.cast_lt.mpr na)) ?_
    refine le_trans (div_le_div_of_nonneg_left (by positivity) (by positivity) (Nat.cast_le.mpr bd)) ?_
    simp only [Nat.cast_pow, Nat.cast_ofNat, â† add_sub_right_comm, zpow_subâ‚€ t0, zpow_natCast,
      â† Nat.cast_add_one, le_refl]
  simp only [â† Nat.cast_le (Î± := â„š), â† ae, mem_Ico, apply_ite (fun n : â„¤ â†¦ (2 : â„š) ^ n),
    apply_ite (fun y : â„š â†¦ y â‰¤ |x|), apply_ite (fun y : â„š â†¦ |x| < y), apply_ite (fun n : â„¤ â†¦ n + 1),
    Nat.cast_mul, Nat.cast_pow, Nat.cast_two, mul_comm (x.den : â„š), â† le_div_iffâ‚€ d0', lo, hi,
    sub_add_cancel]
  by_cases ba : b â‰¤ a
  Â· simp only [Nat.cast_le, ba, ite_true, decide_eq_true_eq, â† Nat.cast_sub ba, Int.toNat_natCast,
    zpow_natCast]
    split_ifs with h
    Â· simp only [h, and_self]
    Â· simp only [not_le.mp h, and_self]
  Â· have ab : a â‰¤ b := (not_le.mp ba).le
    have e : (a : â„¤) - (b : â„¤) = -((b - a : â„•) : â„¤) := by simp only [Nat.cast_sub ab, neg_sub]
    simp [Nat.cast_le, ba, â†“reduceIte, â† Nat.cast_sub ab, mul_comm _ ((2 : â„š) ^ _),
      decide_eq_true_eq, e, zpow_neg, zpow_natCast, ae, not_le,
      inv_le_iff_one_le_mulâ‚€ (two_pow_pos (R := â„š)), â† mul_div_assoc, one_le_div d0',
      div_lt_iffâ‚€ d0', â† div_eq_inv_mul, lt_div_iffâ‚€ (two_pow_pos (R := â„š)), if_true_left, and_self]

lemma Rat.log2_self_le {x : â„š} (x0 : x â‰  0) : 2 ^ x.log2 â‰¤ |x| := (Rat.log2_correct x0).1

lemma Rat.lt_log2_self {x : â„š} : |x| < 2 ^ (x.log2 + 1) := by
  by_cases x0 : x = 0
  Â· simp only [x0, abs_zero, two_zpow_pos]
  Â· exact (Rat.log2_correct x0).2
