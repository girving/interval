import Mathlib.Analysis.Analytic.Constructions
import Mathlib.Analysis.Calculus.FDeriv.Symmetric
import Mathlib.Analysis.Calculus.ParametricIntegral
import Mathlib.Analysis.Complex.RemovableSingularity
import Mathlib.Analysis.SpecialFunctions.ExpDeriv
import Mathlib.Analysis.SpecialFunctions.Integrals
import Mathlib.Analysis.SpecialFunctions.Pow.Real
import Mathlib.Data.Real.Basic
import Mathlib.Topology.ContinuousOn
import Interval.EulerMaclaurin.DerivUnderIntegral
import Interval.EulerMaclaurin.IteratedDerivArith
import Interval.EulerMaclaurin.LHopital
import Interval.EulerMaclaurin.PartialDerivCommute

/-!
# Euler-Maclaurin formula

This lets us approximate finite sums of `C^k` functions with integrals, with known bounds on the
remainder.
-/

open Classical
open Filter
open Set
open Complex (I)
open Function (uncurry)
open MeasureTheory (volume)
open Metric (closedBall)
open scoped Real
open scoped Topology
open intervalIntegral
noncomputable section
namespace EulerMaclaurin

/-!
### The generating function of the Bernoulli polynomials
-/

/-- The generating function of the saw-tooth functions -/
def gen (t x : â„) : â„ :=
  (if t = 0 then 1 else t / (rexp t - 1)) * rexp (x * t)

/-- `gen` for `x = 0`, over complex numbers -/
def genc (t : â„‚) : â„‚ :=
  if t = 0 then 1 else t / (Complex.exp t - 1)

/-- `genc` is analytic in `t`, away from `2Ï€iâ„¤` -/
lemma analyticAt_genc_ae {t : â„‚} (h : Complex.exp t â‰  1) : AnalyticAt â„‚ genc t := by
  rw [analyticAt_congr (g := fun t â†¦ t / (Complex.exp t - 1))]
  Â· apply analyticAt_id.div (analyticAt_cexp.sub analyticAt_const)
    simpa only [Pi.sub_apply, sub_ne_zero] using h
  Â· filter_upwards [ContinuousAt.eventually_ne (Complex.continuous_exp).continuousAt h]
    intro t ne
    have t0 : t â‰  0 := by contrapose ne; simp only [not_not] at ne; simp [ne]
    simp only [genc, t0, if_false]

/-- If `exp t = 1`, it is locally not 1 -/
lemma isolated_exp {z : â„‚} (h : Complex.exp z = 1) : âˆ€á¶  w in ğ“[â‰ ] z, Complex.exp w â‰  1 := by
  simp only [Complex.exp_eq_one_iff, ne_eq, not_exists, eventually_nhdsWithin_iff, mem_compl_iff,
    mem_singleton_iff, Metric.eventually_nhds_iff] at h âŠ¢
  obtain âŸ¨n, hâŸ© := h
  refine âŸ¨2 * Ï€, by positivity, ?_âŸ©
  intro w d
  rw [not_imp_comm, not_forall]
  intro âŸ¨k, eâŸ©
  simp only [not_not] at e
  simp only [e, h, Complex.dist_eq, â† sub_mul, map_mul, Complex.abs_ofNat, Complex.abs_ofReal,
    abs_of_pos Real.pi_pos, Complex.abs_I, mul_one, â† Int.cast_sub, Complex.abs_intCast] at d
  rw [â† lt_div_iffâ‚€ (by positivity), div_self (by positivity), â† Int.cast_abs, â† Int.cast_one,
    Int.cast_lt, Int.abs_lt_one_iff, sub_eq_zero] at d
  simp [e, h, d]

/-- The limit we need, via L'Hopital's rule -/
lemma tendsto_genc_div : Tendsto (fun t â†¦ t / (Complex.exp t - 1)) (ğ“[â‰ ] 0) (ğ“ 1) := by
  nth_rw 2 [(by norm_num : (1 : â„‚) = 1 / 1)]
  apply lhopital_field (f := fun t â†¦ t)
  Â· apply hasDerivAt_id'
  Â· refine HasDerivAt.sub_const ?_ 1
    rw [â† Complex.exp_zero]
    apply Complex.hasDerivAt_exp
  Â· norm_num
  Â· norm_num
  Â· simp

/-- `genc` is continuous at 0 -/
lemma continuousAt_genc : ContinuousAt genc 0 := by
  simp only [â† continuousWithinAt_compl_self, ContinuousWithinAt, genc, Complex.exp_zero,
    â†“reduceIte]
  apply tendsto_genc_div.congr' (fâ‚ := fun t â†¦ t / (Complex.exp t - 1))
  filter_upwards [isolated_exp Complex.exp_zero]
  intro t ne
  have t0 : t â‰  0 := by contrapose ne; simp only [not_not] at ne; simp [ne]
  simp only [genc, t0, if_false]

/-- `genc` is analytic at `0` -/
lemma analyticAt_genc_zero : AnalyticAt â„‚ genc 0 := by
  refine Complex.analyticAt_of_differentiable_on_punctured_nhds_of_continuousAt ?_ continuousAt_genc
  simp only [eventually_nhdsWithin_iff, mem_compl_iff, mem_singleton_iff]
  filter_upwards [eventually_norm_sub_lt 0 Real.two_pi_pos]
  intro t d t0
  refine (analyticAt_genc_ae ?_).differentiableAt
  simp only [ne_eq, Complex.exp_eq_one_iff, not_exists]
  intro n
  by_cases n0 : n = 0
  Â· contrapose t0
    simp only [not_not] at t0
    simp [t0, n0]
  Â· contrapose d
    simp only [not_not] at d
    simp [d, abs_of_pos Real.pi_pos]
    have le : 1 â‰¤ |(n : â„)| := by
      rw [â† Int.cast_one, â† Int.cast_abs, Int.cast_le]
      exact Int.one_le_abs n0
    exact le_mul_of_one_le_left (by bound) le

/-- `genc` is analytic on the real axis -/
lemma analyticAt_genc_real (t : â„) : AnalyticAt â„‚ genc t := by
  by_cases t0 : t = 0
  Â· simp only [t0, Complex.ofReal_zero, analyticAt_genc_zero]
  Â· apply analyticAt_genc_ae
    simpa only [â† Complex.ofReal_exp, â† Complex.ofReal_one, ne_eq, Complex.ofReal_inj,
      Real.exp_eq_one_iff]

/-- `gen` is analytic -/
lemma analyticAt_gen {t x : â„} : AnalyticAt â„ (uncurry gen) (t, x) := by
  apply AnalyticAt.mul
  Â· have e : âˆ€ (d : (x : â„ Ã— â„) â†’ Decidable (x.1 = 0)),
        (fun x : â„ Ã— â„ â†¦ @ite _ (x.1 = 0) (d x) 1 (x.1 / (rexp x.1 - 1))) =
        Complex.reCLM âˆ˜ genc âˆ˜ (fun x : â„ Ã— â„ â†¦ (x.1 : â„‚)) := by
      intro d; ext âŸ¨t, xâŸ©; simp [genc]; split_ifs
      Â· simp only [Complex.one_re]
      Â· simp only [â† Complex.ofReal_one, â† Complex.ofReal_exp, â† Complex.ofReal_sub,
          â† Complex.ofReal_div, Complex.ofReal_re]
    rw [e]
    refine AnalyticAt.comp ?_ (AnalyticAt.comp ?_ ?_)
    Â· apply Complex.reCLM.analyticAt
    Â· simp only [Complex.ofReal_zero]
      exact (analyticAt_genc_real _).restrictScalars
    Â· exact (Complex.ofRealCLM.analyticAt _).comp analyticAt_fst
  Â· exact analyticAt_rexp.comp (analyticAt_snd.mul analyticAt_fst)

/-- `gen` is smooth -/
lemma contDiff_gen : ContDiff â„ âŠ¤ (uncurry gen) := by
  rw [contDiff_iff_contDiffAt]
  intro âŸ¨t,xâŸ©
  exact analyticAt_gen.contDiffAt

/-- `gen` has mean `1` on `x âˆˆ [0,1]` -/
lemma mean_gen (t : â„) : âˆ« x in (0 : â„)..1, gen t x = 1 := by
  by_cases t0 : t = 0
  Â· simp [t0, gen]
  Â· simp only [gen, t0, â†“reduceIte, div_eq_mul_inv, integral_const_mul, ne_eq, not_false_eq_true,
      integral_comp_mul_right, zero_mul, one_mul, integral_exp, Real.exp_zero, smul_eq_mul,
      mul_comm _ tâ»Â¹, â† mul_assoc, inv_mul_cancelâ‚€ t0]
    exact inv_mul_cancelâ‚€ (by simp [sub_eq_zero, t0])

/-- Differentiating w.r.t. `x` pulls out a `t` -/
lemma deriv_gen (t x : â„) : deriv (fun x â†¦ gen t x) x = t * gen t x := by
  generalize hc : (if t = 0 then 1 else t / (rexp t - 1)) = c
  simp [gen, hc, deriv_const_mul_field]
  ring

/-!
### The Bernoulli polynomials
-/

variable {s : â„•}

/-- The Bernoulli polynomials are generated by `gen` -/
def bernoulliPoly (s : â„•) (x : â„) : â„ :=
  iteratedDeriv s (fun t â†¦ gen t x) 0

lemma contDiff_bernoulliPoly : ContDiff â„ âŠ¤ (bernoulliPoly s) :=
  (contDiff_gen.compâ‚‚ contDiff_snd contDiff_fst).iteratedDeriv contDiff_const

@[simp] lemma bernoulliPoly_zero {x : â„} : bernoulliPoly 0 x = 1 := by
  simp [bernoulliPoly, gen]

@[simp] lemma deriv_bernoulliPoly :
    deriv (bernoulliPoly s) = fun x â†¦ s â€¢ bernoulliPoly (s - 1) x := by
  ext x
  rw [(by rfl : bernoulliPoly s = fun y â†¦ bernoulliPoly s y)]
  have comm := deriv_iteratedDeriv_comm (z := (x,0)) (n := s)
    (contDiff_gen.comp (contDiff_snd.prod contDiff_fst))
  simp only [uncurry, Function.comp] at comm
  simp only [bernoulliPoly, comm, deriv_gen]
  clear comm
  have gc : ContDiff â„ âŠ¤ (fun t â†¦ gen t x) := contDiff_gen.compâ‚‚ contDiff_id contDiff_const
  simp only [â† smul_eq_mul, iteratedDeriv_mul (gc.of_le le_top), zero_smul, zero_add]

lemma hasDerivAt_bernoulliPoly {x : â„} :
    HasDerivAt (bernoulliPoly s) (s â€¢ bernoulliPoly (s - 1) x) x := by
  have e : s â€¢ bernoulliPoly (s - 1) x = deriv (bernoulliPoly s) x := by simp
  rw [e, hasDerivAt_deriv_iff]
  exact contDiff_bernoulliPoly.contDiffAt.differentiableAt le_top

/-- Bernoulli polys have mean `n = 0` -/
lemma mean_bernoulliPoly (s : â„•) :
    âˆ« x in (0 : â„)..1, bernoulliPoly s x = if s = 0 then 1 else 0 := by
  simp only [bernoulliPoly]
  rw [â† iteratedDeriv_interval_integral_of_contDiff]
  Â· simp only [mean_gen, iteratedDeriv_const]
  Â· exact contDiff_gen
  Â· exact zero_le_one

/-- The values at 0 and 1 match for `2 â‰¤ s` -/
lemma bernoulliPoly_one_eq_zero (s : â„•) : bernoulliPoly (s + 2) 1 = bernoulliPoly (s + 2) 0 := by
  have nz : (s + 2 : â„) â‰  0 := by linarith
  have e : âˆ€ x, bernoulliPoly (s + 1) x = (s + 2 : â„)â»Â¹ â€¢ deriv (bernoulliPoly (s + 2)) x := by
    intro x
    simp only [deriv_bernoulliPoly, Nat.add_one_sub_one, nsmul_eq_mul, Nat.cast_add, Nat.cast_ofNat,
      smul_eq_mul, â† mul_assoc, inv_mul_cancelâ‚€ nz, one_mul]
  have m := mean_bernoulliPoly (s + 1)
  simp only [e, smul_eq_mul, intervalIntegral.integral_const_mul, Nat.add_one_ne_zero,
    if_false, mul_eq_zero, inv_ne_zero nz, false_or] at m
  rwa [intervalIntegral.integral_deriv_eq_sub, sub_eq_zero] at m
  Â· intro x _
    exact (contDiff_bernoulliPoly.differentiable le_top).differentiableAt
  Â· apply ContinuousOn.intervalIntegrable
    exact (contDiff_bernoulliPoly.continuous_deriv le_top).continuousOn

@[simp] lemma bernoulliPoly_one {x : â„} : bernoulliPoly 1 x = x - 1 / 2 := by
  generalize hc : bernoulliPoly 1 0 = c
  have i : âˆ€ y, bernoulliPoly 1 y = y + c := by
    intro y
    rw [â† hc, â† sub_eq_iff_eq_add, â† intervalIntegral.integral_deriv_eq_sub]
    Â· simp only [deriv_bernoulliPoly, Nat.sub_self, bernoulliPoly_zero, one_smul, integral_const,
        sub_zero, smul_eq_mul, mul_one]
    Â· intro x _
      exact (contDiff_bernoulliPoly.differentiable le_top).differentiableAt
    Â· simp only [deriv_bernoulliPoly, Nat.sub_self, bernoulliPoly_zero, one_smul,
        _root_.intervalIntegrable_const]
  have m := mean_bernoulliPoly 1
  simp only [i, intervalIntegrable_id, _root_.intervalIntegrable_const, integral_add, integral_id,
    one_pow, ne_eq, OfNat.ofNat_ne_zero, not_false_eq_true, zero_pow, sub_zero, one_div,
    integral_const, smul_eq_mul, one_mul, one_ne_zero, â†“reduceIte] at m
  simp only [i, one_div]
  linarith

/-!
### The presaw functions

These are `saw s x` smoothly extended from a particular `[a,a+1)` interval.
-/

variable {a : â„¤}

/-- The saw functions are scaled, shifted versions of the Bernoulli polynomials -/
def presaw (s : â„•) (a : â„¤) (x : â„) : â„ :=
  (s.factorial : â„)â»Â¹ â€¢ bernoulliPoly s (x - a)

/-- `presaw` is smooth -/
lemma contDiff_presaw : ContDiff â„ âŠ¤ (presaw s a) := by
  exact (contDiff_bernoulliPoly.comp (contDiff_id.sub contDiff_const)).const_smul _

@[simp] lemma presaw_start {x : â„} : presaw 0 a x = 1 := by simp [presaw]

lemma hasDerivAt_presaw {x : â„} : HasDerivAt (presaw (s + 1) a) (presaw s a x) x := by
  have e : presaw (s + 1) a = (fun x â†¦ presaw (s + 1) a x) := rfl
  simp only [presaw, Nat.factorial_succ, mul_comm _ s.factorial, Nat.cast_mul,
    mul_inv, â† smul_smul, e]
  apply HasDerivAt.const_smul
  have s0 : ((s + 1 : â„•) : â„) â‰  0 := by simp only [Nat.cast_ne_zero]; omega
  have sc : s = s + 1 - 1 := by omega
  rw [â† inv_smul_smulâ‚€ s0 (x := (bernoulliPoly s (x - â†‘a)))]
  nth_rw 5 [sc]
  apply HasDerivAt.const_smul
  rw [Nat.cast_smul_eq_nsmul]
  rw [â† mul_one (((s + 1) â€¢ bernoulliPoly (s + 1 - 1) (x - â†‘a)))]
  exact HasDerivAt.comp _ (hasDerivAt_bernoulliPoly (s := s + 1)) (h := fun x : â„ â†¦ x - a)
    ((hasDerivAt_id' x).sub_const _)

@[simp] lemma deriv_presaw {x : â„} : deriv (presaw (s + 1) a) x = presaw s a x := by
  rw [hasDerivAt_presaw.deriv]

@[simp] lemma presaw_zero {x : â„} : presaw 0 a x = 1 := by
  simp only [presaw, Nat.factorial_zero, Nat.cast_one, inv_one, bernoulliPoly_zero, smul_eq_mul,
    mul_one]

/-!
### The saw functions
-/

/-- The saw functions are scaled, periodic versions of the Bernoulli polynomials -/
def saw (s : â„•) (x : â„) :=
  (s.factorial : â„)â»Â¹ â€¢ bernoulliPoly s (Int.fract x)

/-- Saw is nice on `[a,a+1)` -/
lemma saw_eqOn {a : â„¤} :
    EqOn (saw s) (presaw s a) (Ico a (a+1)) := by
  intro x m
  simp only [Int.cast_add, Int.cast_one, mem_Ico, â† Int.floor_eq_iff] at m
  simp only [saw, presaw, smul_eq_mul, mul_eq_mul_left_iff, inv_eq_zero, Nat.cast_eq_zero,
    Int.fract, m]

/-- `presaw` at integer values in terms of `saw` -/
@[simp] lemma presaw_coe_same {a : â„¤} : presaw s a a = saw s 0 := by
  rw [â† saw_eqOn (a := a)]
  all_goals simp [saw]

/-- `presaw` at integer values in terms of `saw` -/
@[simp] lemma presaw_coe_succ {a : â„¤} : presaw (s + 2) a (a + 1) = saw (s + 2) 0 := by
  simp only [presaw, Int.cast_add, Int.cast_one, add_sub_cancel_left, bernoulliPoly_one_eq_zero,
    smul_eq_mul, saw, Int.fract_zero]

/-- `presaw` at integer values in terms of `saw` -/
@[simp] lemma presaw_one_coe_succ {a : â„¤} : presaw 1 a (a + 1) = 1 / 2 := by
  simp only [presaw, Nat.factorial_one, Nat.cast_one, inv_one, Int.cast_add, Int.cast_one,
    add_sub_cancel_left, bernoulliPoly_one, one_div, smul_eq_mul, one_mul]
  norm_num

/-- Saw is nice on `[a,a+1)` -/
lemma contDiff_saw : ContDiffOn â„ âŠ¤ (saw s) (Ico a (a+1)) := by
  rw [contDiffOn_congr saw_eqOn]
  exact contDiff_presaw.contDiffOn

@[simp] lemma saw_zero {x : â„} : saw 0 x = 1 := by
  simp only [saw, Nat.factorial_zero, Nat.cast_one, inv_one, bernoulliPoly_zero, smul_eq_mul,
    mul_one]

@[simp] lemma saw_int {x : â„¤} : saw s x = saw s 0 := by
  simp only [saw, Int.fract_intCast, smul_eq_mul, Int.fract_zero]

lemma hasDerivAt_saw {s : â„•} {a : â„¤} {x : â„} (m : x âˆˆ Ioo (a : â„) (a + 1)) :
    HasDerivAt (saw (s + 1)) (saw s x) x := by
  have e : saw (s + 1) =á¶ [ğ“ x] (fun x â†¦ presaw (s + 1) a x) := by
    apply saw_eqOn.eventuallyEq_of_mem
    exact Ico_mem_nhds_iff.mpr m
  refine HasDerivAt.congr_of_eventuallyEq ?_ e
  simp only [saw_eqOn (mem_Ico_of_Ioo m), Nat.factorial_succ, mul_comm _ s.factorial, Nat.cast_mul,
    mul_inv, â† smul_smul]
  exact hasDerivAt_presaw

@[simp] lemma deriv_saw {x : â„} (m : x âˆˆ Ioo (a : â„) (a + 1)) :
    deriv (saw (s + 1)) x = saw s x := by
  rw [(hasDerivAt_saw m).deriv]

/-- `saw 1` is a saw-tooth function -/
lemma saw_one {x : â„} : saw 1 x = Int.fract x - 1 / 2 := by
  simp only [saw, Nat.factorial_one, Nat.cast_one, inv_one, bernoulliPoly_one, one_div, smul_eq_mul,
    one_mul]

@[simp] lemma saw_one_zero : saw 1 0 = -2â»Â¹ := by
  simp only [saw_one, Int.fract_zero, one_div, zero_sub]

/-- `saw` is continuous for `2 â‰¤ s` -/
lemma continuous_saw : Continuous (saw (s + 2)) := by
  rw [continuous_iff_continuousAt]
  intro x
  set a := âŒŠxâŒ‹
  by_cases xa : x = a
  Â· simp only [xa, continuousAt_iff_continuous_left'_right']
    constructor
    Â· apply ContinuousWithinAt.congr_of_eventuallyEq (f := presaw (s + 2) (a - 1))
      Â· exact contDiff_presaw.continuous.continuousWithinAt
      Â· apply saw_eqOn.eventuallyEq_of_mem
        apply Ico_mem_nhdsWithin_Iio
        simp only [Int.cast_sub, Int.cast_one, sub_add_cancel, mem_Ioc, sub_lt_self_iff,
          zero_lt_one, le_refl, and_self]
      Â· nth_rw 2 [â† sub_add_cancel (a : â„) 1]
        rw [saw_int, â† Int.cast_one (R := â„), â† Int.cast_sub, Int.cast_one, presaw_coe_succ]
    Â· apply ContinuousWithinAt.congr_of_eventuallyEq (f := presaw (s + 2) a)
      Â· exact contDiff_presaw.continuous.continuousWithinAt
      Â· apply saw_eqOn.eventuallyEq_of_mem
        apply Ico_mem_nhdsWithin_Ioi
        simp only [Int.cast_add, Int.cast_one, mem_Ico, le_refl, lt_add_iff_pos_right, zero_lt_one,
          and_self]
      Â· simp only [saw_int, presaw_coe_same]
  Â· apply ContinuousAt.congr_of_eventuallyEq (f := presaw (s + 2) a)
    Â· exact contDiff_presaw.continuous.continuousAt
    Â· apply saw_eqOn.eventuallyEq_of_mem
      apply Ico_mem_nhds
      Â· exact (Ne.symm xa).lt_of_le (Int.floor_le x)
      Â· simp only [Int.cast_add, Int.cast_one, Int.lt_floor_add_one, a]

/-!
### Euler-Maclaurin for a single `[a, a + 1]` interval
-/

variable {E : Type*} [NormedAddCommGroup E] [NormedSpace â„ E]
variable {f : â„ â†’ E} {t : Set â„} {a b c : â„¤} {n : â„•}

lemma intervalIntegrable_presaw_smul (fc : ContDiffOn â„ n f t) {a b : â„} (ab : a â‰¤ b)
    (u : UniqueDiffOn â„ t) (abt : Icc a b âŠ† t) {s : â„•} :
    IntervalIntegrable (fun x â†¦ presaw s c x â€¢ iteratedDerivWithin n f t x) volume a b := by
  refine (contDiff_presaw.continuous.continuousOn.smul ?_).intervalIntegrable
  simp only [uIcc_of_le ab]
  exact (fc.continuousOn_iteratedDerivWithin (le_refl _) u).mono abt

lemma integral_saw_eq_integral_presaw :
    âˆ« x : â„ in a..a + 1, saw s x â€¢ iteratedDerivWithin s f t x =
    âˆ« x : â„ in a..a + 1, presaw s a x â€¢ iteratedDerivWithin s f t x := by
  apply intervalIntegral.integral_congr_ae
  have e : Î™ (a : â„) (a + 1)  =áµ[volume] Ioo a (a + 1) := by
    rw [uIoc_of_le]
    Â· exact MeasureTheory.Ioo_ae_eq_Ioc.symm
    Â· simp only [Int.cast_le, (lt_add_one _).le]
  simp only [â† MeasureTheory.ae_restrict_iff' measurableSet_uIoc,
    MeasureTheory.ae_restrict_congr_set e, MeasureTheory.ae_restrict_iff' measurableSet_Ioo]
  filter_upwards
  intro x m
  rw [saw_eqOn (a := a)]
  exact Ioo_subset_Ico_self m

/-- The key integration by parts identity, `presaw` version -/
lemma presaw_smul_iteratedDeriv_by_parts [CompleteSpace E] (fc : ContDiffOn â„ (s+1) f t)
    (u : UniqueDiffOn â„ t) (abt : Icc (a : â„) (a + 1) âŠ† t) :
    âˆ« x in a..(a + 1), presaw s c x â€¢ iteratedDerivWithin s f t x =
      presaw (s+1) c (a + 1) â€¢ iteratedDerivWithin s f t (a + 1) -
      presaw (s+1) c a â€¢ iteratedDerivWithin s f t a -
      âˆ« x in a..(a + 1), presaw (s+1) c x â€¢ iteratedDerivWithin (s+1) f t x := by
  have i0 := intervalIntegrable_presaw_smul (s := s) (c := c) fc.of_succ (by linarith) u abt
  have i1 := intervalIntegrable_presaw_smul (s := s + 1) (c := c) fc (by linarith) u abt
  rw [eq_sub_iff_add_eq, â† intervalIntegral.integral_add i0 i1]
  set g := fun x â†¦ presaw (s + 1) c x â€¢ iteratedDerivWithin s f t x
  set g' := fun x â†¦ presaw s c x â€¢ iteratedDerivWithin s f t x +
    presaw (s + 1) c x â€¢ iteratedDerivWithin (s + 1) f t x
  have df : âˆ€ x âˆˆ Ioo (a : â„) (a + 1) \ âˆ…, HasDerivAt g (g' x) x := by
    intro x m
    simp only [diff_empty] at m
    simp only [g, g', add_comm (presaw s c _ â€¢ _) _]
    apply HasDerivAt.smul
    Â· exact hasDerivAt_presaw
    Â· have mt := abt (Ioo_subset_Icc_self m)
      apply HasDerivWithinAt.hasDerivAt
      Â· rw [iteratedDerivWithin_succ, hasDerivWithinAt_derivWithin_iff]
        Â· apply (fc.contDiffWithinAt mt).differentiableWithinAt_iteratedDerivWithin
          Â· simp only [â† Nat.cast_add_one, Nat.cast_lt, Nat.lt_add_one]
          Â· simp only [mt, insert_eq_of_mem, u]
        Â· exact u x mt
      Â· exact Filter.monotone_mem (subset_trans Ioo_subset_Icc_self abt) (isOpen_Ioo.mem_nhds m)
  refine Eq.trans (MeasureTheory.integral_eq_of_hasDerivWithinAt_off_countable_of_le
    (f := g) (f' := g') (Hd := df) (by linarith) countable_empty ?_ ?_) ?_
  Â· apply contDiff_presaw.continuous.continuousOn.smul
    exact (fc.continuousOn_iteratedDerivWithin le_self_add u).mono abt
  Â· exact i0.add i1
  Â· simp only [g, smul_sub]

/-- Iterated integration by parts, `presaw` version -/
lemma presaw_iterated_by_parts [CompleteSpace E] (fc : ContDiffOn â„ (s+1) f t)
    (u : UniqueDiffOn â„ t) (abt : Icc (a : â„) (a + 1) âŠ† t) :
    âˆ« x in a..a + 1, f x = (2â»Â¹ : â„) â€¢ (f a + f (a + 1)) -
      âˆ‘ m âˆˆ Finset.range s, (-1 : â„) ^ m â€¢ saw (m + 2) 0 â€¢
        (iteratedDerivWithin (m + 1) f t (a + 1) - iteratedDerivWithin (m + 1) f t a) -
      (-1 : â„) ^ s â€¢ âˆ« x in a..a + 1, presaw (s+1) a x â€¢ iteratedDerivWithin (s+1) f t x := by
  induction' s with s h
  Â· simpa only [zero_add, presaw_one_coe_succ, presaw_coe_same, saw_one_zero, neg_smul,
      sub_neg_eq_add, one_div, iteratedDerivWithin_zero, presaw_zero, smul_add, Finset.range_zero,
      Finset.sum_empty, sub_zero, â† smul_add, Int.reduceNeg, pow_zero, one_smul, Int.cast_add,
      Int.cast_one, add_comm _ (f a)] using presaw_smul_iteratedDeriv_by_parts fc u abt (c := a)
  Â· refine (h fc.of_succ).trans ?_
    clear h
    have z : (-1 : â„) ^ s â‰  0 := by
      simp only [ne_eq, pow_eq_zero_iff', neg_eq_zero, one_ne_zero, false_and, not_false_eq_true]
    simp only [sub_sub, add_left_cancel_iff, sub_right_inj, Finset.sum_range_succ, add_assoc,
      â† smul_smul, pow_succ, â† smul_add, neg_smul, â† sub_eq_add_neg, one_smul, smul_right_inj z]
    refine (presaw_smul_iteratedDeriv_by_parts fc u abt (c := a)).trans ?_
    simp only [(by omega : s + 1 + 1 = s + 2), presaw_coe_same, Nat.reduceAdd, sub_left_inj,
      presaw_coe_succ, â† smul_sub]

/-!
### The full Euler-Maclaurin formula
-/

variable {n : â„•}

/-- Trapezoidal sum: the trapezoidal rule for integer step size on `[a, a + n]` -/
def trapezoid_sum (f : â„ â†’ E) (a : â„¤) : â„• â†’ E
  | 0 => 0
  | n + 1 => trapezoid_sum f a n + (2â»Â¹ : â„) â€¢ (f (a + n) + f (a + n + 1))

-- Definitions as accessible lemmas
@[simp] lemma trapezoid_sum_zero : trapezoid_sum f a 0 = 0 := rfl
lemma trapezoid_sum_succ :
    trapezoid_sum f a (n + 1) = trapezoid_sum f a n + (2â»Â¹ : â„) â€¢ (f (a + n) + f (a + n + 1)) :=
  rfl

lemma ae_ne {Î± : Type*} [MeasurableSpace Î±] (Î¼ : MeasureTheory.Measure Î±) [MeasureTheory.NoAtoms Î¼]
    (x : Î±) : âˆ€áµ y âˆ‚Î¼, y â‰  x := by
  simp only [Filter.Eventually, ne_eq, MeasureTheory.mem_ae_iff, compl_ne_eq_singleton,
    MeasureTheory.measure_singleton]

lemma intervalIntegrable_saw_smul (fc : ContDiffOn â„ s f t) (u : UniqueDiffOn â„ t)
    (abt : Icc (a : â„) (a + n) âŠ† t) :
    IntervalIntegrable (fun x â†¦ saw s x â€¢ iteratedDerivWithin s f t x)
      volume a (a + n) := by
  induction' n with n h
  Â· simp
  Â· simp only [Nat.cast_add, Nat.cast_one, â† add_assoc]
    refine (h (subset_trans (Icc_subset_Icc (by rfl) (by simp)) abt)).trans ?_
    have ab1 : Icc ((a + n : â„¤) : â„) ((a + n : â„¤) + 1) âŠ† t := by
      refine subset_trans (Icc_subset_Icc ?_ ?_) abt
      all_goals simp [â† add_assoc]
    have i := intervalIntegrable_presaw_smul (s := s) (c := a + n) fc (by simp) u ab1
    simp only [Int.cast_add, Int.cast_natCast, intervalIntegrable_iff, le_add_iff_nonneg_right,
      zero_le_one, uIoc_of_le] at i âŠ¢
    apply i.congr
    simp only [MeasureTheory.ae_restrict_iff' measurableSet_Ioc, Filter.EventuallyEq]
    filter_upwards [ae_ne volume (a + n + 1 : â„)]
    intro x ne m
    rw [saw_eqOn (a := a + n)]
    simp only [mem_Ioc, ne_eq, Int.cast_add, Int.cast_natCast, mem_Ico] at m ne âŠ¢
    exact âŸ¨m.1.le, Ne.lt_of_le ne m.2âŸ©

/-- The Euler-Maclaurin formula -/
theorem sum_eq_integral_add [CompleteSpace E] (fc : ContDiffOn â„ (s + 1) f t)
    (u : UniqueDiffOn â„ t) (abt : Icc (a : â„) (a + n) âŠ† t) :
    trapezoid_sum f a n = (âˆ« x in a..a + n, f x) +
      âˆ‘ m âˆˆ Finset.range s, (-1 : â„) ^ m â€¢ saw (m + 2) 0 â€¢
        (iteratedDerivWithin (m + 1) f t (a + n) - iteratedDerivWithin (m + 1) f t a) +
      (-1 : â„) ^ s â€¢ âˆ« x in a..a + n, saw (s+1) x â€¢ iteratedDerivWithin (s+1) f t x := by
  induction' n with n h
  Â· simp only [trapezoid_sum_zero, CharP.cast_eq_zero, add_zero, integral_same, sub_self,
      smul_zero, Finset.sum_const_zero]
  Â· generalize hb : a + n = b
    have hb' : (a + n : â„) = b := by simp [â† hb]
    have ab0 : Icc (a : â„) (a + n) âŠ† t := subset_trans (Icc_subset_Icc (by rfl) (by simp)) abt
    have ab1 : Icc (a + n : â„) (a + n + 1) âŠ† t :=
      subset_trans (Icc_subset_Icc (by simp) (by simp [add_assoc])) abt
    simp only [hb', Nat.cast_add, Nat.cast_one, â† add_assoc] at ab0 ab1 h âŠ¢
    simp only [trapezoid_sum_succ, â† add_assoc, Nat.cast_add_one, Int.cast_add,
      Int.cast_natCast, Int.cast_one]
    rw [â† intervalIntegral.integral_add_adjacent_intervals (b := a + n),
      â† intervalIntegral.integral_add_adjacent_intervals (a := a) (b := b) (c := b + 1), h ab0]
    Â· simp only [presaw_iterated_by_parts (a := b) fc u ab1, â† integral_saw_eq_integral_presaw,
        smul_sub, smul_add, Finset.sum_sub_distrib, hb']
      abel
    Â· rw [â† hb']
      exact intervalIntegrable_saw_smul (by simpa) u (by simpa only [hb'])
    Â· rw [â† Nat.cast_one (R := â„)]
      exact intervalIntegrable_saw_smul (by simpa) u (by simpa only [Nat.cast_one])
    Â· exact (fc.continuousOn.mono (by simpa only [hb'])).intervalIntegrable_of_Icc (by linarith)
    Â· exact (fc.continuousOn.mono (by simpa only [hb'])).intervalIntegrable_of_Icc (by linarith)
