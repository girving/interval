import Mathlib.Algebra.EuclideanDomain.Basic
import Mathlib.Algebra.EuclideanDomain.Field
import Mathlib.Analysis.Calculus.Deriv.Basic
import Mathlib.Analysis.RCLike.Basic
import Mathlib.Data.Complex.Abs
import Mathlib.Tactic.Bound

/-!
# L'Hopital's rule over any normed field
-/

open Filter
open Set
open scoped Topology

variable {ğ•œ : Type} [NontriviallyNormedField ğ•œ]

@[bound] lemma norm_sub_norm_le_norm_add {E : Type*} [SeminormedAddGroup E] (x y : E) :
    â€–xâ€– - â€–yâ€– â‰¤ â€–x + yâ€– := by simpa using norm_sub_norm_le x (-y)

attribute [bound] norm_add_le

/-- L'HÃ´pital's rule using derivatives at a point, general field version -/
theorem lhopital_field {f g : ğ•œ â†’ ğ•œ} {a f' g' : ğ•œ} (df : HasDerivAt f f' a) (dg : HasDerivAt g g' a)
    (g'0 : g' â‰  0) (f0 : f a = 0) (g0 : g a = 0) :
    Tendsto (fun x => f x / g x) (ğ“[â‰ ] a) (ğ“ (f' / g')) := by
  simp only [Metric.tendsto_nhds, NormedAddGroup.dist_eq, eventually_nhdsWithin_iff,
    mem_compl_iff, mem_singleton_iff]
  intro e ep
  simp only [hasDerivAt_iff_isLittleO, f0, sub_zero, smul_eq_mul, Asymptotics.isLittleO_iff,
    g0] at df dg
  have g'p : 0 < â€–g'â€– := norm_pos_iff.mpr g'0
  generalize hb : 2 * (1 + â€–f' / g'â€–) / â€–g'â€– = b
  generalize hc : min (e / 2 / b) (2â»Â¹ * â€–g'â€–) = c
  have b0 : 0 < b := by
    rw [â† hb]
    refine div_pos (mul_pos (by norm_num) ?_) g'p
    exact add_pos_of_pos_of_nonneg (by bound) (by bound)
  have c0 : 0 < c := by bound
  have cb : c * b < e := by
    simp only [â† hc]
    calc min (e / 2 / b) (2â»Â¹ * â€–g'â€–) * b
      _ â‰¤ (e / 2 / b) * b := by bound
      _ = e / 2 := by field_simp [b0.ne']; ring
      _ < e := by bound
  have cg : c â‰¤ 2â»Â¹ * â€–g'â€– := by bound
  clear hc
  filter_upwards [df c0, dg c0]
  intro x fx gx xa
  generalize hy : x - a = y at fx gx
  have y0 : y â‰  0 := by simpa only [â† hy, sub_ne_zero]
  have yp : 0 < â€–yâ€– := norm_pos_iff.mpr y0
  have lo : â€–g xâ€– â‰¥ 2â»Â¹ * â€–g'â€– * â€–yâ€– := by
    calc â€–g xâ€–
      _ = â€–y * g' + (g x - y * g')â€– := by ring_nf
      _ â‰¥ â€–y * g'â€– - â€–g x - y * g'â€– := by bound
      _ â‰¥ â€–y * g'â€– - c * â€–yâ€– := by bound
      _ = (â€–g'â€– - c) * â€–yâ€– := by simp only [sub_mul, norm_mul, mul_comm]
      _ â‰¥ (â€–g'â€– - 2â»Â¹ * â€–g'â€–) * â€–yâ€– := by bound
      _ = 2â»Â¹ * â€–g'â€– * â€–yâ€– := by ring
  have dg0 : g x â‰  0 := by
    contrapose lo
    simp only [ne_eq, not_not] at lo
    simp only [lo, norm_zero, ge_iff_le, not_le]
    bound
  calc â€–f x / g x - f' / g'â€–
    _ = â€–(f x - y * f') / g x + (y * f' / g x - f' / g')â€– := by ring_nf
    _ â‰¤ â€–(f x - y * f') / g xâ€– + â€–y * f' / g x - f' / g'â€– := by bound
    _ = â€–(f x - y * f')â€– / â€–g xâ€– + â€–y * f' / g x - f' / g'â€– := by simp only [norm_div]
    _ â‰¤ c * â€–yâ€– / â€–g xâ€– + â€–y * f' / g x - f' / g'â€– := by bound
    _ = (c * â€–yâ€– + â€–y * f' - f' / g' * g xâ€–) / â€–g xâ€– := by
        simp only [â† norm_div, sub_div, mul_div_cancel_rightâ‚€ _ dg0, add_div]
    _ = (c * â€–yâ€– + â€–y * f' - f' / g' * (g x - y * g' + y * g')â€–) / â€–g xâ€– := by rw [sub_add_cancel]
    _ = (c * â€–yâ€– + â€–y * f' - f' / g' * (g x - y * g') - y * f' / g' * g'â€–) / â€–g xâ€– := by ring_nf
    _ = (c * â€–yâ€– + â€–f' / g'â€– * â€–g x - y * g'â€–) / â€–g xâ€– := by
        simp only [div_mul_cancelâ‚€ _ g'0, sub_sub_cancel_left, norm_neg, norm_mul, norm_div]
    _ â‰¤ (c * â€–yâ€– + â€–f' / g'â€– * (c * â€–yâ€–)) / â€–g xâ€– := by bound
    _ = c * â€–yâ€– * (1 + â€–f' / g'â€–) / â€–g xâ€– := by ring
    _ â‰¤ c * â€–yâ€– * (1 + â€–f' / g'â€–) / (2â»Â¹ * â€–g'â€– * â€–yâ€–) := by bound
    _ = c * b * â€–yâ€– / â€–yâ€– := by rw [â† hb]; ring_nf
    _ = c * b := by rw [mul_div_cancel_rightâ‚€ _ yp.ne']
    _ < e := by bound
